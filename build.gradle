plugins {
    id 'java'
    id 'java-library'
    alias(handylibs.plugins.kotlin.jvm)
    alias(handylibs.plugins.kotlin.serialization)
    alias(handylibs.plugins.dokka)
    alias(handylibs.plugins.javamodularity)

    id 'application'
    id 'distribution'
    alias(handylibs.plugins.hidetake.ssh)
    alias(handylibs.plugins.javafx)
    alias(handylibs.plugins.jlink)
}

allprojects {

    group = 'uk.iatom.handydisplay'
    version = '0.0.1'

    apply plugin: handylibs.plugins.kotlin.jvm.get().pluginId
    apply plugin: handylibs.plugins.kotlin.serialization.get().pluginId
    apply plugin: handylibs.plugins.javafx.get().pluginId
    apply plugin: handylibs.plugins.dokka.get().pluginId
    apply plugin: handylibs.plugins.javamodularity.get().pluginId
    apply plugin: 'java'
    apply plugin: 'java-library'

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        testImplementation handylibs.kotlin.test
        implementation handylibs.kotlinx.json
        implementation handylibs.kotlinx.properties
    }

    test {
        useJUnitPlatform()
    }

    compileKotlin {
        kotlinOptions.jvmTarget = '17'
    }

    compileTestKotlin {
        kotlinOptions.jvmTarget = '17'
    }
}

dependencies {
    implementation handylibs.fxgl
    implementation handylibs.picocli
}

application {
    mainClass = "uk.iatom.handydisplay.MainKt"
    mainModule = "iAtom.HandyDisplay"
}

javafx {
    version = JAVAFX_VERSION
    modules = [ 'javafx.controls', 'javafx.fxml' , 'javafx.media']
}

jlink {
    launcher {
        name = 'uk.iatom.handydisplay.core'
    }
    forceMerge('kotlin')
    forceMerge('javafx')
}